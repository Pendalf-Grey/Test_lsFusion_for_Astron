MODULE Main;

REQUIRE Time;

// Перечень складов
CLASS Stock 'Склад';

// Первичные свойства для склада
name 'Наименование' = DATA STRING (Stock);
address 'Адрес' = DATA STRING (Stock);

// Интерфейс склада
FORM stocks 'Склады'
    OBJECTS s = Stock
    PROPERTIES (s) name, address, NEW, DELETE
;

// Перечень товаров
CLASS Item 'Товар';

// Первичные свойства товаров
name 'Наименование' = DATA STRING[50] (Item);
barcode 'Штрихкод' = DATA STRING[13] (Item);
salePrice 'Цена продажи' = DATA NUMERIC[15,2] (Item);

// Интерфейс товара
FORM items 'Товары'
    OBJECTS i = Item
    PROPERTIES (i) name, barcode, salePrice, NEW, DELETE
;

// Перечень организаций
CLASS LegalEntity 'Организация';

// Первичные свойства организации
name 'Наименование' = DATA STRING (LegalEntity);
inn 'ИНН' = DATA STRING (LegalEntity);

// Валидация по уникальному имени
// Вторичное свойство
legalEntity 'Организация' (STRING inn) = GROUP AGGR LegalEntity le BY inn(le);

// Интерфейс организации
FORM legalEntities 'Организации'
    OBJECTS le = LegalEntity
    PROPERTIES (le) name, inn, NEW, DELETE
;

CLASS Receipt 'Приход';

number 'Номер' = DATA INTEGER (Receipt);
date 'Дата' = DATA DATE (Receipt);

// Локальное событие
// Чтобы при открытии формы она уже была заполнена текущим числом
WHEN LOCAL SET(Receipt r IS Receipt) DO date(r) <- DATE(currentDateTime());

legalEntity 'Организация' = DATA LegalEntity (Receipt);
nameLegalEntity 'Организация' (Receipt r) = name(legalEntity(r));

stock 'Склад' = DATA Stock (Receipt);
nameStock 'Склад' (Receipt r) = name(stock(r));

CLASS ReceiptLine 'Строка прихода';

receipt 'Документ' = DATA Receipt (ReceiptLine) NONULL DELETE;

index '№№' (ReceiptLine l) = PARTITION SUM 1 IF l IS ReceiptLine ORDER l BY receipt(l);

item 'Товар' = DATA Item (ReceiptLine);
nameItem 'Товар' (ReceiptLine l) = name(item(l));

quantity 'Количество' = DATA NUMERIC[15,3] (ReceiptLine);

FORM receipts 'Приходы'
    OBJECTS r = Receipt
    PROPERTIES (r) READONLY number, date, nameLegalEntity, nameStock
    PROPERTIES (r) NEWSESSION NEW, EDIT, DELETE
;

FORM receipt 'Приход'
    OBJECTS r = Receipt PANEL
    PROPERTIES (r) number, date, nameLegalEntity, nameStock

    OBJECTS l = ReceiptLine
    PROPERTIES (l) index, nameItem, quantity, NEW, DELETE
    FILTERS receipt(l) = r

    EDIT Receipt OBJECT r
;

CLASS Shipment 'Отгрузка';

number 'Номер' = DATA INTEGER (Shipment);
date 'Дата' = DATA DATE (Shipment);

// Локальное событие
// Чтобы при открытии формы она уже была заполнена текущим числом
WHEN LOCAL SET(Shipment s IS Shipment) DO date(s) <- DATE(currentDateTime());

legalEntity 'Организация' = DATA LegalEntity (Shipment);
nameLegalEntity 'Организация' (Shipment s) = name(legalEntity(s));

stock 'Склад' = DATA Stock (Shipment);
nameStock 'Склад' (Shipment s) = name(stock(s));

CLASS ShipmentLine 'Строка отгрузки';

shipment 'Документ' = DATA Shipment (ShipmentLine) NONULL DELETE;

index '№№' (ShipmentLine l) = PARTITION SUM 1 IF l IS ShipmentLine ORDER l BY shipment(l);

item 'Товар' = DATA Item (ShipmentLine);
nameItem 'Товар' (ShipmentLine l) = name(item(l));

quantity 'Количество' = DATA NUMERIC[15,3] (ShipmentLine);

FORM shipments 'Расходы'
    OBJECTS s = Shipment
    PROPERTIES (s) READONLY number, date, nameLegalEntity, nameStock
    PROPERTIES (s) NEWSESSION NEW, EDIT, DELETE
;

FORM shipment 'Расход'
    OBJECTS s = Shipment PANEL
    PROPERTIES (s) number, date, nameLegalEntity, nameStock

    OBJECTS l = ShipmentLine
    PROPERTIES (l) index, nameItem, quantity, NEW, DELETE
    FILTERS shipment(l) = s

    EDIT Shipment OBJECT s
;

inQuantity 'Приход' (Stock s, Item i) = GROUP SUM quantity(ReceiptLine rl) BY stock(receipt(rl)), item(rl);
outQuantity 'Расход' (Stock s, Item i) = GROUP SUM quantity(ShipmentLine sl) BY stock(shipment(sl)), item(sl);

balance 'Остаток' (Stock s, Item i) = inQuantity(s, i) (-) outQuantity(s, i);

FORM balance 'Остатки'
    OBJECTS (s = Stock, i = Item)
    PROPERTIES READONLY name(s), name(i), balance(s, i)
    ORDERS name(s), name(i)
    FILTERS balance(s, i) > 0
;

// Основное меню
NAVIGATOR {
    // Разделы
    NEW FOLDER masterData 'НСИ' FIRST WINDOW toolbar {
        NEW stocks;
        NEW items;
        NEW legalEntities;
    }
    NEW FOLDER documents 'Документы' AFTER masterData WINDOW toolbar {
        NEW receipts;
        NEW shipments;
    }
    NEW balance AFTER documents;
}